program        → declaration* EOF ;

// DECLARATIONS

declaration    → classDecl
               | interfaceDecl
               | funDecl
               | varDecl
               | statement ;

classDecl      → ( "dynamic" )? "class" ( typeParam )? IDENTIFIER ( "<" IDENTIFIER )? ( "<<" IDENTIFIER )?
                 "{" method* "}" ;
interfaceDecl  → "interface" ( typeParam )? IDENTIFIER ( "<<" IDENTIFIER ( "," IDENTIFIER )* )?
                 "{" abstractMethod* ")" ;
funDecl        → "fun" function ;
varDecl        → "var" IDENTIFIER ( "=" expression )? ( ":" type )? ";" ;

// STATEMENTS

statement      → exprStmt
               | forStmt
               | ifStmt
               | returnStmt
               | whileStmt
               | breakStmt
               | continueStmt
               | importStmt
               | block ;

exprStmt       → expression ";" ;
forStmt        → "for" "(" ( varDecl | exprStmt | ";" )
                           expression? ";"
                           expression? ")" statement ;
ifStmt         → "if" "(" expression ")" statement
                 ( "else" statement )? ;
returnStmt     → "return" expression? ";" ;
whileStmt      → "while" "(" expression ")" statement ;
block          → "{" declaration* "}" ;
continueStmt   → "continue;" ;
breakStmt      → "break;" ;
importStmt     → "import" STRING ";" ;

// EXPRESSIONS

expression     → assignment ;

assignment     → ( call "." )? IDENTIFIER "=" assignment
               | logic_or ;

logic_or       → logic_and ( "or" logic_and )* ;
logic_and      → check ( "and" check )* ;
check          → cast ( "is" | "isnt" ) cast ;
cast           → equality ( "as" ) equality ;
equality       → comparison ( ( "!=" | "==" ) comparison )* ;
comparison     → term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term           → factor ( ( "-" | "+" ) factor )* ;
factor         → unary ( ( "/" | "*" ) unary )* ;

unary          → ( "!" | "-" ) unary | call ;
call           → primary ( "(" arguments? ")" | "." IDENTIFIER )* ;
primary        → "true" | "false" | "nil" | "this"
               | NUMBER | STRING | IDENTIFIER ( typeParam )? | "(" expression ")"
               | "super" "." IDENTIFIER ( typeParam )? ;

// UTILITY

method         → ( "static" | "native" )? "fun" methodSignature block? ;
abstractMethod → ( "static" | "native" )? "fun" methodSignature ;
methodSignature→ IDENTIFIER "(" parameters? ")" ( ":" type )? ;
function       → IDENTIFIER "(" parameters? ")" ( ":" type )? block ;
parameters     → IDENTIFIER ( ":" type )? ( "," IDENTIFIER ( ":" type )? )* ;
arguments      → expression ( "," expression )* ;
type           → ( "any" | "nil" | "number" | "boolean" | "string" | "function" | "class" ( typeParam )? | IDENTIFIER ( typeParam )? ( "|" type* )? )
typeParam      → "<" IDENTIFIER ( ( "," IDENTIFIER )* )? ">"

// LEXICAL

NUMBER         → DIGIT+ ( "." DIGIT+ )? ;
STRING         → "\"" <any char except "\"">* "\"" ;
IDENTIFIER     → ALPHA ( ALPHA | DIGIT )* ;
ALPHA          → "a" ... "z" | "A" ... "Z" | "_" ;
DIGIT          → "0" ... "9" ;